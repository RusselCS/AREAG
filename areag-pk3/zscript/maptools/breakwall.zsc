class BreakableWall : BasicActor
{
    Default
    {
        //$Category AREAG-Map Tools
        //$Title Breakable Wall
        //$Sprite E0WCK0
        //+BasicActor.SHOWHITBOX;
        +NOGRAVITY;
        +SOLID;
        +SHOOTABLE;
        +NOBLOOD;
        +NOTARGETSWITCH;
        +DONTFALL;
        mass 99999999999999;
        painchance "Normal", 0;
        painchance "Explosive", 256;
        Height 32;
        Radius 16;
        Scale 2.5;
    }

    static const String[] classTypes = {
        "Basic", // None
        "Outskirts", // Outskirts
        "Gateway", // Gateway
        "Study", // Study
        "Impact", // Impact
        "Duungeon", // Dungeon
        "Shadows"  // Shadows
	};

    int user_frame;
    int user_radius;
    int user_height;

    int spc;
    int args0;
    int args1;
    int args2;
    int args3;
    int args4;

    override void BasicInit() {
        Super.BasicInit();
        scale.x = 1.0;
        scale.y = 1.0;

        if(user_height == 0) {
            user_height = height;
        }
        if(user_radius == 0) {
            user_radius = radius;
        }

        spc = special;
        args0 = args[0];
        args1 = args[1];
        args2 = args[2];
        args3 = args[3];
        args4 = args[4];

        special = 0;
        args[0] = 0;
        args[1] = 0;
        args[2] = 0;
        args[3] = 0;
        args[4] = 0;
        
        A_SetSize(user_radius, user_height);

        int lvl = level.levelnum;
        if (lvl < 0 || lvl >= classTypes.size()) {
            lvl = 0;
        }
        
        if(user_frame != 0) {
            String modelName = string.format("BreakableWall_%s_%d", classTypes[lvl], user_frame);
            let model = A_SpawnBasicEffect(modelname);
            model.master = self;
        }        
    }

    States
    {
    Spawn:
        TNT1 A 1;
        wait;
    Pain:
        TNT1 A 0 A_CallSpecial(spc, args0, args1, args2, args3, args4);
        TNT1 A 0 Thing_Destroy(tid, false, 0);
        TNT1 A 1;
        wait;
    Death:
        TNT1 AAAAAAAAAAAAAAA 1 A_SpawnItemEx("BreakableWallPuff", fRandRange(user_radius/2), fRandRange(user_radius/2), frandom(0, user_height));
        stop;
    
    Placeholder:
        BWOS A 0;
        BWGW A 0;
        BWQS A 0;
        BWLI A 0;
        BWKD A 0;
        BWRS A 0;
        stop;
    }
}

class BreakableWallPuff : BasicEffect
{
    Default
    {
        -NOINTERACTION;
        +FORCEXYBILLBOARD;
        +ROLLSPRITE;
        -ACTIVATEIMPACT;
        +MOVEWITHSECTOR;
        bouncetype "hexen";
        alpha 0.4;
        scale 1.5;
    }
	States
	{
	Spawn:
		WHPF A 0;
		WHPF A 0 A_SetRoll(frandom(0.0, 360.0));
		SHPF ABCDEFGH 1;
		stop;
	}
}

class BreakableWallModel : BasicEffect
{
    Default
    {
        +BasicActor.DIEWITHMASTER;
        Renderstyle "Normal";
    }

    States
    {
    Spawn:
        BWOS A 1;
        wait;
    Death:
        BWOS A 1 A_FadeOut();
        wait;
    }
}

// Lost Impact
class BreakableWall_Impact_1 : BreakableWallModel {}
class BreakableWall_Impact_2 : BreakableWallModel {}
class BreakableWall_Impact_3 : BreakableWallModel {}
class BreakableWall_Impact_4 : BreakableWallModel {}
class BreakableWall_Impact_5 : BreakableWallModel {}

actor AREAGDrone : AREAGPlayer
{
	Height 24
	Radius 12
	player.viewheight 20
	player.morphweapon "DronePlasmaGun"
	Player.AttackZOffset 5 // 41 - view height
}

actor DronePickup : BasicInventory 
{
	inventory.pickupmessage ""
	inventory.pickupsound "upgrade/treasure"
	+INVENTORY.AUTOACTIVATE
	+FLOATBOB
	+NOGRAVITY
	Height 64 // tall
	Scale 1.0
	States
	{
	Spawn:
		SDRO C 1
		loop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteAlways("pickups_displayMessage", 0, PICKTYPE_TREASURE, PICKMSG_SPIDERDRONE)
		TNT1 A 0 A_GiveInventory("DroneTrigger", 1)
		stop
	}
}

actor DroneUsedFlag : Once {}
actor DroneTrigger : BasicTreasureItem
{
	inventory.icon "INICSPID"
	States
	{
	TreasureUse:
		TNT1 A 0 A_GiveInventory("LowerWeapon", 1)
		TNT1 A 0 A_GiveInventory("UsedItem", 1)
		TNT1 A 0 A_GiveInventory("DroneUsedFlag", 1)
		fail
	}
}

actor DroneFlag : Once {}
actor DroneActivate : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_GiveInventory("DroneFlag", 1)
		TNT1 A 0 ACS_NamedExecuteAlways("drone_activate", 0)
		stop
	}
}

actor DroneTransitionMissile : BasicProjectile
{
	PROJECTILE
	+THRUACTORS
	+BLOCKASPLAYER
	-NOGRAVITY
	gravity 0.6
	bouncetype "Doom"
	bouncefactor 0.0
	wallbouncefactor 0.0
	Damage (0)
	Speed 12
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ThrustThingZ(0, 20, 0, 0)
		TNT1 A 0 A_TransferPointer(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_DEFAULT, AAPTR_MASTER)
		SDRO A 8
		SDRO B 1
		wait
	}
}

actor DronePlasmaGun : Weapon
{
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoType "Clip"
	Obituary "$OB_MPPISTOL"
	+WEAPON.WIMPY_WEAPON
	Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
	Tag "$TAG_PISTOL"
	States
	{
	Ready:
		SDRW A 1 A_WeaponReady
		Loop
	Deselect:
		SDRW A 1 A_Lower
		Loop
	Select:
		SDRW A 1 A_Raise
		Loop
	Fire:
		SDRW B 0 A_PlaySoundEx("weapon/dronelaser", "weapon")
		SDRW B 0 A_FireCustomMissile("DroneActivator", 0, 0, 0, 0)
		SDRW B 0 A_FireCustomMissile("DroneLaser", 0, 0, -8, 0)
		SDRW B 0 A_FireCustomMissile("DroneLaser", 0, 0, 8, 0)
		SDRW B 2 BRIGHT
		SDRW CD 4
		SDRW A 8
		Goto Ready
	}
}

actor DroneLaser : BasicFastProjectile
{
	PROJECTILE
	+BLOODSPLATTER
	-ACTIVATEIMPACT
	+FORCEXYBILLBOARD
	missiletype "DroneLaserTrail"
	missileheight 8
	renderstyle add
	Radius 6
	Height 7
	damage (5)
	speed 45
	scale 0.2
	reactiontime 10
	States
	{
	Spawn:
		DLAZ A 1 A_Countdown
		loop
	}
}

actor DroneActivator : DroneLaser
{
	Damage 0
	renderstyle none
	missiletype ""
	+ACTIVATEIMPACT
}

actor DroneLaserTrail : BasicEffect
{
	+FORCEXYBILLBOARD
	renderstyle add
	scale 0.2
	States
	{
	Spawn:
		DLAZ AAA 1
		stop
	}
}

actor DronePlayerDummy
{
	-SOLID
	+NOCLIP
	renderstyle translucent
	Alpha 1.0
	States
	{
	Spawn:
		PLAY A 1
		loop
	}
}

actor GearGreavesPickup : BasicInventory
{
	inventory.pickupmessage ""
	inventory.pickupsound "upgrade/artifice"
	+INVENTORY.AUTOACTIVATE
	+FLOATBOB
	+NOGRAVITY
	Height 64 // tall
	Scale 2.5
	States
	{
	Spawn:
		BOOT G 1
		loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("GearGreavesSpeed", 1, "PickupFail")
		TNT1 A 0 ACS_NamedExecuteAlways("pickups_displayMessage", 0, PICKTYPE_ARTIFICE, PICKMSG_GEAR)
		TNT1 A 0 A_GiveInventory("GearGreavesSpeed", 1)
		TNT1 A 0 A_GiveInventory("BaseMoveSpeed", 25)
		stop
	PickupFail:
		TNT1 A 0
		fail
	}
}

actor GearGreavesSpeed : Once {}

const int DASH_SPEED = 20;
const int DASH_WATER = 15;

actor PressFrames : Powerup
{
	powerup.duration 20
}

actor PressedLeft : PressFrames {}
actor PressedRight : PressFrames {}
actor PressedForward : PressFrames {}
actor PressedBack : PressFrames {}

actor DashHUD : PowerUp { powerup.duration 25 }

actor DashCooldown : Powerup
{
	powerup.duration 50
}

actor AirDashCooldown : DashCooldown {}

actor DashForward : Custominventory
{
	+INVENTORY.AUTOACTIVATE
	States
	{
	Use:
		TNT1 A 0 A_JumpIfInventory("OnGround", 1, "Ground")
		goto Air
	Ground:
		TNT1 A 0 A_GiveInventory("DashCooldown", 1)
		goto DashStart
	Air:
		TNT1 A 0 A_GiveInventory("AirDashCooldown", 1)
		goto DashStart
	DashStart:
		TNT1 A 0 A_PlaySoundEx("player/stepdash", "SoundSlot7")
		TNT1 A 0 A_GiveInventory("DashHUD", 1)
		TNT1 A 0 A_JumpIfInventory("LegsUnderwater", 1, "WaterDash")
		TNT1 A 0 A_ChangeVelocity(momx, momy, (momz>0)*momz, CVF_REPLACE)
		TNT1 A 0 A_JumpIf(true, "Dash")
		goto Dash
	Dash:
		TNT1 A 0 A_ChangeVelocity(DASH_SPEED, 0, 0, CVF_RELATIVE)
		stop
	WaterDash:
		TNT1 A 0 A_ChangeVelocity(DASH_WATER, 0 ,0, CVF_RELATIVE)
		stop
	}
}

actor DashBackward : DashForward
{
	States
	{
	Dash:
		TNT1 A 0 A_ChangeVelocity(-DASH_SPEED, 0 ,0, CVF_RELATIVE)
		stop
	WaterDash:
		TNT1 A 0 A_ChangeVelocity(-DASH_WATER, 0 ,0, CVF_RELATIVE)
		stop
	}
}

actor DashRight : DashForward
{
	States
	{
	Dash:
		TNT1 A 0 A_ChangeVelocity(0, -DASH_SPEED, 0, CVF_RELATIVE)
		stop
	WaterDash:
		TNT1 A 0 A_ChangeVelocity(0, -DASH_WATER, 0, CVF_RELATIVE)
		stop
	}
}

actor DashLeft : DashForward
{
	States
	{
	Dash:
		TNT1 A 0 A_ChangeVelocity(0, DASH_SPEED, 0, CVF_RELATIVE)
		stop
	WaterDash:
		TNT1 A 0 A_ChangeVelocity(0, DASH_WATER, 0, CVF_RELATIVE)
		stop
	}
}

actor SteamGreavesPickup : GearGreavesPickup
{
	States
	{
	Spawn:
		BOOT H 1
		loop
	Pickup:
		TNT1 A 0 A_JumpIfInventory("SteamGreavesFlag", 1, "PickupFail")
		TNT1 A 0 ACS_NamedExecuteAlways("pickups_displayMessage", 0, PICKTYPE_ARTIFICE, PICKMSG_STEAM)
		TNT1 A 0 A_GiveInventory("SteamGreavesFlag", 1)
		TNT1 A 0 A_GiveInventory("BaseJumpSpeed", 250)
		stop
	PickupFail:
		TNT1 A 0
		fail
	}
}

actor SteamGreavesFlag : Once {}

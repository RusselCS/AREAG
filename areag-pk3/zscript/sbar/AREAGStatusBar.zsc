Class AREAGStatusBar : BaseStatusBar {

    AREAGPlayerPawn pl;

    HUDFont sBarFont;

    override void Init() {
		Super.Init();
		SetSize(0, 160, 100);
        Font fnt = "INDEXFONT";
        sBarFont = HUDFont.Create(fnt, fnt.GetCharWidth("0"), Mono_CellLeft);
	}

    override void Tick() {
        Super.Tick();

        if(!pl) {
            pl = AREAGPlayerPawn(CPlayer.mo);
        }
    }

    override void Draw(int state, double TicFrac) {
        Super.Draw(state, TicFrac);
        
        if (pl && (state == HUD_StatusBar || state == HUD_Fullscreen))
        {
            BeginHUD();
            AREAGDrawHud(TicFrac);
        }
    }

    void AREAGDrawHud(double TicFrac) {

        double yOffsDiff = 14.0;
        double yOffs = (5*yOffsDiff + yOffsDiff/2);
        double xOffs = 12.0;
        double barYOffs = 2.0;
        double barXOffs = 30.5;

        double hpXOffs = 12.0;

        double numXOffs = 23.0;
        double numYOffs = 7.0;

        double subXOffs = 14.0;

        double subCXOffs = 8.0;
        double subCYOffs = 4.0;

        int amt, maxamt;

        DrawImage("ICOCSH",  (xOffs, -yOffs));
        [amt, maxamt] = GetAmount("AGCoin");
        DrawString(sBarFont, FormatNumber(amt, 4), (xOffs + numXOffs, -yOffs-numYOffs), DI_TEXT_ALIGN_RIGHT);

        yOffs -= yOffsDiff;

        DrawInventoryIcon(pl.InvSel, (xOffs, -yOffs));

        if(pl.iSelectedWep >= 0) {
            let swep = pl.subs[pl.iSelectedWep];
            [amt, maxamt] = GetAmount(swep.getAmmoType());
            DrawInventoryIcon(swep.getInvAmmo(),  (xOffs + subXOffs, -yOffs));
            DrawString(sBarFont, FormatNumber(amt, 3), (xOffs + subXOffs + subCXOffs, -yOffs-subCYOffs), DI_TEXT_ALIGN_RIGHT);
            DrawString(sBarFont, "/", (xOffs + subXOffs + subCXOffs*2, -yOffs-subCYOffs), DI_TEXT_ALIGN_RIGHT);
            DrawString(sBarFont, FormatNumber(maxamt, 3), (xOffs + subXOffs + subCXOffs*3, -yOffs-subCYOffs), DI_TEXT_ALIGN_LEFT);
        }

        yOffs -= yOffsDiff;

        DrawImage("ICOHP",  (xOffs, -yOffs));
        for(int i = 1; i <= pl.countInv("MaxHealthBars"); i++) {
            DrawImage("BAREHP", (xOffs + hpXOffs*i, -yOffs - barYOffs));

            if(pl.countInv("HealthBars") >= i) {

                String fullBar;
				[amt, maxamt] = GetAmount("HealthCounter");

                if(pl.countInv("ArmorBars") >= i) {
                    fullBar = "BARSHP";
                } else {
                    fullBar = "BARVHP";
                }

                if(i == pl.countInv("HealthBars")) {
                    DrawBar(fullBar, "", amt, maxamt, (xOffs + hpXOffs*i, -yOffs - barYOffs), 0, SHADER_HORZ);
                } else {
                    if(pl.countInv("ArmorBars") >= i) {
                        DrawImage("BARSHP", (xOffs + hpXOffs*i, -yOffs - barYOffs));
                    } else {
                        DrawImage("BARVHP", (xOffs + hpXOffs*i, -yOffs - barYOffs));
                    }
                }
            }
        }

        yOffs -= yOffsDiff;
		[amt, maxamt] = GetAmount("BreathTime");
        DrawImage("ICOBUB",  (xOffs, -yOffs));
        DrawBar("BARBUB", "BAREMP", amt, maxamt, (xOffs + barXOffs, -yOffs - barYOffs), 0, SHADER_HORZ);

        yOffs -= yOffsDiff;
		[amt, maxamt] = GetAmount("DroneBattery");
        DrawImage("ICOBAT",  (xOffs, -yOffs));
        DrawBar("BARBAT", "BAREMP", amt, maxamt, (xOffs + barXOffs, -yOffs - barYOffs), 0, SHADER_HORZ);
    }
}

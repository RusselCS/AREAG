class ZFBoxImage : ZFElement {
	protected ZFBoxTextures boxTex;
	ZFBoxTextures getBoxTex() { return self.boxTex; }
	void setBoxTex(ZFBoxTextures boxTex) { self.boxTex = boxTex; }
	protected ZFBoxDrawer boxDrawer;

	protected Vector2 boxScale;
	Vector2 getBoxScale() { return self.boxScale; }
	void setBoxScale(Vector2 boxScale) { self.boxScale = boxScale; }

	void config(ZFBoxTextures textures = NULL, Vector2 scale = (1, 1)) {
		setBoxTex(textures);
		setBoxScale(scale);
		setAlpha(1.0);
	}

	static ZFBoxImage create(Vector2 pos, Vector2 size, ZFBoxTextures textures = NULL, Vector2 scale = (1, 1)) {
		let ret = new('ZFBoxImage');

		ret.config(textures, scale);
		ret.setBox(pos, size);

		return ret;
	}

	override void drawer() {
		if (hidden) { return; }

		ZFAABB beforeClip, clipRect;
		screenClip(beforeClip, clipRect);
		Screen.setClipRect(int(clipRect.pos.x), int(clipRect.pos.y), int(clipRect.size.x), int(clipRect.size.y));

		boxDrawer.draw(self, (0, 0), box.size, boxTex, true, boxScale);

		Screen.setClipRect(int(beforeClip.pos.x), int(beforeClip.pos.y), int(beforeClip.size.x), int(beforeClip.size.y));
	}
}

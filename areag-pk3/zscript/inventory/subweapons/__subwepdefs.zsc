const SWEP_NONE = -1;

class SubWeaponPickup : BasicInventory
{
    class<Ammo> cAmmoType;
    int iAmmoGive;
    String sWeaponName;
    Ammo aAmmo;
 
    property AmmoType: cAmmoType;
    property AmmoGive: iAmmoGive;
    property WeaponName: sWeaponName;

    action void A_AREAGGiveSubWeapon() {
        if(invoker.inWorld) {
            AREAGPlayerPawn plr = AREAGPlayerPawn(self);
            GiveInventory(invoker.cAmmoType, invoker.iAmmoGive);
            invoker.aAmmo = Ammo(self.FindInventory(invoker.cAmmoType));
            SubWeapon wep = invoker.subStateBuilder();
            plr.pushSubWeapon(wep);
        } else {
            A_Log("PICKUP FAILED!!");
        }
    }

    virtual SubWeapon subStateBuilder() {
        StateLabel rS = "Raise";

        return buildSubWeapon(rS);
    }

    SubWeapon buildSubWeapon(StateLabel rS)
    {
        return new("SubWeapon").Init(cAmmoType, sWeaponName, rS, aAmmo);
    }

    States
    {
    PickupEffect:
        TNT1 A 0 A_AREAGGiveSubWeapon();
        stop;
    }
}

class SubWeapon
{
    class<Ammo> ammoType;
    String weaponName;
    StateLabel raiseState;
    Ammo invAmmo;

    SubWeapon Init(class<Ammo> aT, String sN, StateLabel rS, Ammo iA) {
        ammoType = aT;
        weaponName = sN;
        raiseState = rS;
        invAmmo = iA;
        return self;
    }

    String getWeaponName() {
        return weaponName;
    }

    class<Ammo> getAmmoType() {
        return ammoType;
    }

    Ammo getInvAmmo() {
        return invAmmo;
    }

    StateLabel getRaiseState() {
        return raiseState;
    }
}

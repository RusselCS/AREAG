class BreakableWall : BasicActor
{
    Default
    {
        //$Category AREAG-Map Tools
        //$Title Breakable Wall
        //$Sprite E0WCK0
        //$Arg0 Model ID
        +BasicActor.SHOWHITBOX;
        +NOGRAVITY;
        +SOLID;
        +SHOOTABLE;
        +NOBLOOD;
        +NOTARGETSWITCH;
        +DONTFALL;
        mass 99999999999999;
        painchance "Normal", 0;
        painchance "Explosive", 256;
        Height 32;
        Radius 16;
        Scale 2.5;
    }

    static const String[] waitStates = {
        "NUM1", // None
        "BWOS", // Outskirts
        "BWGW", // Gateway
        "BWQS", // Study
        "BWLI", // Impact
        "BWKD", // Dungeon
        "BWRS" // Shadows
	};

    override void BasicInit() {
        Super.BasicInit();
        scale.x = 1.0;
        scale.y = 1.0;

        int lvl = level.levelnum;
        if (lvl < 0 || lvl >= waitStates.size()) {
            lvl = 0;
        }
        
        self.sprite = GetSpriteIndex(name(waitStates[lvl]));
    }

    override void BasicTick() {
        super.BasicTick();

        self.frame = args[0];
    }

    States
    {
    Spawn:
        TNT1 A 1;
        wait;
    Pain:
        TNT1 A 0 Thing_Destroy(tid, false, 0);
        TNT1 A 1;
        wait;
    Death:
        TNT1 AAAAAAAAAAAAAAA 1 A_SpawnItemEx("TribalShieldPuff", frandom(-10, 10), frandom(-10, 10), 16+frandom(-10, 10));
        stop;
    
    Placeholder:
        BWOS A 0;
        BWGW A 0;
        BWQS A 0;
        BWLI A 0;
        BWKD A 0;
        BWRS A 0;
        stop;
    }
}

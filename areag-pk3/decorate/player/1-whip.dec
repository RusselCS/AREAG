actor AdventureWhip : SubWeapon
{
	Weapon.SlotNumber 1
	Obituary "%o was whipped into shape by %k's Adventurer's Whip."
	Inventory.Pickupmessage "Retrieved your trusty whip!"
	States
	{
	Ready:
		WHP1 A 0 A_TakeInventory("LowerWeapon", 1)
		WHP1 DCBA 1
		goto ReadyFrame
	Deselect:
		WHP1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		WHP1 A 1 A_Lower
		Loop
	Select:
		WHP1 A 0 A_GunFlash // Subweapons
		WHP1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		WHP1 A 1 A_Lower
		Loop
	Fire:
		WHP1 A 0 A_JumpIfInventory("LowerWeapon", 1, "Hold")
		WHP1 A 0 A_GiveInventory("LowerWeapon", 1)
		WHP1 AABCD 1
	Hold:
		TNT1 A 1
		TNT1 A 0 A_Refire
		Goto Swing
	Swing:
		WHP1 A 0 A_JumpIfInventory("SwingRight", 1, "SwingRight")
	SwingLeft:
		WHPL A 0 A_PlaySoundEx("weapon/whipswing", "weapon")
		WHPL ABC 1 A_SetAngle(angle+0.2)
		WHPL A 0 A_FireCustomMissile("WhipProjectileL", 0, 0, 0)
		WHPL DEZ 1 A_SetAngle(angle+0.2)
		WHPL A 0 A_GiveInventory("SwingRight", 1)
		TNT1 A 6
		TNT1 AAAAAAAAAA 1 A_WeaponReady(WRF_DISABLESWITCH)
		goto Ready
	SwingRight:
		WHPL A 0 A_PlaySoundEx("weapon/whipswing", "weapon")
		WHPL FGH 1 A_SetAngle(angle-0.2)
		WHPL A 0 A_FireCustomMissile("WhipProjectileR", 0, 0, 0)
		WHPL IJK 1 A_SetAngle(angle-0.2)
		WHPL F 0 A_TakeInventory("SwingRight", 1)
		TNT1 A 6
		TNT1 AAAAAAAAAA 1 A_WeaponReady(WRF_DISABLESWITCH)
		goto Ready
	// Framework states
	// Specify to tell the base weapon which idle sprite to use!
	AltfireWindup:
		WHP1 A 0 A_JumpIfInventory("LowerWeapon", 1, "AltfireHold")
		WHP1 A 0 A_GiveInventory("LowerWeapon", 1)
		WHP1 AABCD 1
		Goto AltfireHold
	ReadyFrame:
		WHP1 A 0
		goto WeaponReadyLoop
	LowerWeapon:
		WHP1 AABCD 1
		goto LowerCheck
	}
}

actor SwingRight : Once {}

actor WhipLash : Ammo
{
	inventory.amount 1
	inventory.maxamount 20
}

actor WhipProjectileL : BasicFastProjectile
{
	Speed 40
	Damage (20)
	+NOTIMEFREEZE
	+HITTRACER
	+BLOODSPLATTER
	radius 4
	radius 2
	States
	{
	SpawnLoop:
		TNT1 A 1
		TNT1 A 1
		TNT1 A 0 A_PlaySoundEx("weapon/whipcrack", "body")
		TNT1 A 0 A_AlertMonsters(512)
		stop
	XDeath:
		TNT1 A 0 A_PlaySoundEx("weapon/whipstrike", "body")
		TNT1 A 0 A_SpawnItemEx("WhipQuake")
		stop
	Crash:
	Death:
		TNT1 A 0 A_PlaySoundEx("weapon/whipwall", "body")
		TNT1 AAA 0 A_SpawnItemEx("WhipPuff", 0, 0, 0, frandom(0.4, 1.0), 0, frandom(-1.0, 1.0), frandom(0.0, 360.0))
		TNT1 A 0 A_SpawnItemEx("WhipQuake")
		stop
	}
}

actor WhipProjectileR : WhipProjectileL {}

actor WhipQuake
{
	+NOINTERACTION
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 Radius_Quake(1, 4, 0, 32, 0)
		TNT1 A 1
		stop
	}
}

actor WhipPuff : BasicEffect
{
	-NOINTERACTION
	+FORCEXYBILLBOARD
	+ROLLSPRITE
	-ACTIVATEIMPACT
	+MOVEWITHSECTOR
	bouncetype hexen
	alpha 0.4
	scale 1.5
	States
	{
	Spawn:
		WHPF A 0
		WHPF A 0 A_SetRoll(frandom(0.0, 360.0))
		WHPF ABCD 2
		stop
	}
}

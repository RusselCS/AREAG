class TestBossTorch : StoneBody
{
    Default
    {
        +SOLID;
        +SHOOTABLE;
        +NOBLOOD;
        +CANPASS;
        +FORCEYBILLBOARD;
        height 62;
        radius 15;
        scale 2.0;
        mass 999999999;
        health 3;
        gravity 0.6;
        species "Monster";
    }

    action void A_SpawnTorchDebris()
    {
        A_FaceTarget();
        A_StartSound("misc/torchbreak", CHAN_BODY);

        BasicEffect act;
        act = A_SpawnBasicEffect("TestBossTorchShard", 0, 0, 54, -frandom(1.5, 3)*invoker.gravity, -frandom(2.5, 7.5*invoker.gravity), frandom(2, 6)*invoker.gravity);
        act.score = 0;
        act.rollspeed = -0.4;
        act.gravity = invoker.gravity;
        act = A_SpawnBasicEffect("TestBossTorchShard", 0, 4, 38, frandom(1, 3)*invoker.gravity, frandom(2.5, 7.5)*invoker.gravity, frandom(1, 2)*invoker.gravity);
        act.score = 1;
        act.rollspeed = 0.4;
        act.gravity = invoker.gravity;
        act = A_SpawnBasicEffect("TestBossTorchShard", 0, -2, 26, -frandom(1.5, 4.5)*invoker.gravity, frandom(2.5, 7.5)*invoker.gravity, -frandom(1, 2)*invoker.gravity);
        act.score = 2;
        act.rollspeed = 0.8;
        act.gravity = invoker.gravity;
        act = A_SpawnBasicEffect("TestBossTorchShard", 0, 4, 24, frandom(2, 6)*invoker.gravity, -frandom(2.5, 7.5)*invoker.gravity, 0);
        act.score = 3;
        act.rollspeed = -0.8;
        act.gravity = invoker.gravity;
        A_SpawnBasicEffect("TestBossTorchBase");
    }

    States
    {
    Spawn:
        TRC1 ABCD 4;
        loop;
    Death:
        TRC1 I 0 A_SpawnTorchDebris();
        stop;
    }
}

class TestBossTorchEmber : BasicEffect
{
    States
    {
    Spawn:
        EFIR ABCDEFG 1;
        stop;
    }
}

class TestBossTorchShard : BasicEffect
{
    Default
    {
        -NOGRAVITY;
        +ROLLSPRITE;
        -NOINTERACTION;
        scale 2.0;
        gravity 0.6;
    }

    override void BasicTick() {
        if(score == 0 && GetAge() % 2 == 0) {
            A_SpawnItemEx("TestBossTorchEmber", frandrange(8), frandrange(8), 16);
        }

        if(z-Floorz <= 0) {
            forceDeath();
        }
    }

    action State A_SetTorchFrame() {
        switch(invoker.score) {
            default:
                return ResolveState("SpawnE");
            case 1:
                return ResolveState("SpawnF");
            case 2:
                return ResolveState("SpawnG");
            case 3:
                return ResolveState("SpawnH");
        }
    }

    States
    {
    Spawn:
        TNT1 A 0;
        TNT1 A 0 A_SetTorchFrame();
    SpawnE:
        TRC1 E -1;
    SpawnF:
        TRC1 F -1;
    SpawnG:
        TRC1 G -1;
    SpawnH:
        TRC1 H -1;
        stop;
    Death:
        TNT1 A 0;
        stop;
    }
}

class TestBossTorchBase : BasicEffect
{
    Default
    {
        +FORCEYBILLBOARD;
        Scale 2.0;
    }

    States
    {
    Spawn:
        TRC1 I 1;
        loop;
    }
}

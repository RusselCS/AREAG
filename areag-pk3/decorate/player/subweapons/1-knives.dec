// THROWING KNIVES
actor KnifeEquipped : Once {}
actor KnifeStock : Counter {}
actor KnifeMax : Counter {}

actor KnifePouchPickup : CustomInventory
{
	inventory.pickupmessage ""
	inventory.pickupsound "upgrade/offense"
	+INVENTORY.AUTOACTIVATE
	+FLOATBOB
	+NOGRAVITY
	Height 64 // tall
	Scale 2.5
	States
	{
	Spawn:
		SUBK A 1
		loop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteAlways("pickups_displayMessage", 0, PICKTYPE_SUBWEAPON, PICKMSG_KNIFE)
		TNT1 A 0 A_GiveInventory("KnifeMax", 10)
		TNT1 A 0 A_GiveInventory("KnifeStock", 10)
		TNT1 A 0 A_GiveInventory("SetKnife", 1)
		stop
	}
}

actor SetKnife : SetSub
{
	States
	{
	Equip:
		TNT1 A 0 A_GiveInventory("KnifeEquipped")
		stop
	}
}

actor ThrowKnife : BasicFastProjectile
{
	PROJECTILE
	+BLOODSPLATTER
	+FORCEXYBILLBOARD
	missiletype "KnifeTrail"
	missileheight 8
	Radius 6
	Height 7
	damage (15)
	speed 100
	scale 2.0
	States
	{
	Spawn:
		SUBK W 1
		loop
	Death:
		SUBK W 0 A_PlaySoundEx("weapon/knifewall", "Weapon")
		SUBK W 0 A_SpawnItemEx("KnifeDeath", 0, 0, 0)
		SUBK W 0 A_AlertMonsters(96)
		stop
	Crash:
	XDeath:
		SUBK W 0 A_PlaySound("weapon/knifehit", CHAN_AUTO, 1.0, false, ATTN_NONE)
		SUBK W 0
		stop
	}
}

actor KnifeTrail : BasicEffect
{
	scale 2.0
	renderstyle Add
	+FORCEXYBILLBOARD
	alpha 0.8
	States
	{
	Spawn:
		SUBK X 1 A_FadeOut(0.2)
		wait
	}
}

actor KnifeDeath : BasicEffect
{
	-NOINTERACTION
	+FORCEXYBILLBOARD
	scale 2.0
	height 1
	radius 1
	States
	{
	Spawn:
		SUBK W 0
		SUBK W 0 A_CheckFloor("Floor")
		SUBK W 0 A_JumpIf(ceilingz - z < 16, "Ceiling")
		SUBK W 0 A_ChangeFlag(NOINTERACTION, true)
		SUBK W 0 A_SetAngle(angle+frandom(-18.0, 18.0))
		SUBK W 0 A_ChangeVelocity(-10, 0, 5, CVF_REPLACE|CVF_RELATIVE)
		SUBK W 1 A_SetAngle(angle+45)
		SUBK W 0 A_FadeOut(0.05)
		SUBK W 0 A_ChangeVelocity(0, 0, -1)
		goto Spawn+6
	Floor:
		SUBK W 0 A_ChangeFlag(NOINTERACTION, true)
		SUBK W 0 A_SetAngle(angle+frandom(-10.0, 10.0))
		SUBK W 0 A_ChangeVelocity(10, 0, 5, CVF_REPLACE|CVF_RELATIVE)
		SUBK W 1 A_SetAngle(angle+45)
		SUBK W 0 A_FadeOut(0.05)
		goto Floor+3
	Ceiling:
		SUBK W 0 A_ChangeFlag(NOINTERACTION, true)
		SUBK W 0 A_SetAngle(angle+frandom(-10.0, 10.0))
		SUBK W 0 A_ChangeVelocity(10, 0, 0, CVF_REPLACE|CVF_RELATIVE)
		SUBK W 1 A_SetAngle(angle+45)
		SUBK W 0 A_FadeOut(0.05)
		goto Ceiling+3
	}
}

actor KnifeAmmo : CustomInventory
{
	inventory.pickupsound "ammo/pickup"
	inventory.pickupmessage ""
	Scale 2.5
	States
	{
	Spawn:
		SUBK Z 1
		wait
	Pickup:
		TNT1 A 0 A_JumpIf(CallACS("status_ammo_check", SUB_KNIFE), "Success")
		fail
	Success:
		TNT1 A 0 ACS_NamedExecuteAlways("status_ammo", 0, SUB_KNIFE, 1)
		stop
	}
}
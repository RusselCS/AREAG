// DYNAMITE
actor DynamiteEquipped : Once {}
actor DynamiteStock : Counter {}
actor DynamiteMax : Counter {}

actor DynamiteCratePickup : BasicInventory
{
	inventory.pickupmessage ""
	inventory.pickupsound "upgrade/offense"
	+INVENTORY.AUTOACTIVATE
	+FLOATBOB
	+NOGRAVITY
	Height 64 // tall
	Scale 1.0
	States
	{
	Spawn:
		SUBD A 1
		loop
	Pickup:
		TNT1 A 0 ACS_NamedExecuteAlways("pickups_displayMessage", 0, PICKTYPE_SUBWEAPON, PICKMSG_DYNAMITE)
		TNT1 A 0 A_GiveInventory("DynamiteMax", 10)
		TNT1 A 0 A_GiveInventory("DynamiteStock", 10)
		TNT1 A 0 A_GiveInventory("SetDynamite", 1)
		stop
	}
}

actor SetDynamite : SetSub
{
	States
	{
	Equip:
		TNT1 A 0 A_GiveInventory("DynamiteEquipped")
		stop
	}
}

actor DynamiteProjectile : BasicProjectile
{
	PROJECTILE
	+RIPPER
	-NOGRAVITY
	gravity 1.2
	wallbouncefactor 0.8
	bouncefactor 0.4
	bouncecount 3
	height 10
	radius 5
	speed 40
	bouncetype doom
	States
	{
	Spawn:
		SUBD BCDEFGHI 1
		loop
	Death:
		SUBD B 0 A_SpawnItemEx("DynamiteMine")
		stop
	}
}

actor DynamiteMine : BasicProjectile
{
	PROJECTILE
	+THRUACTORS
	+SHOOTABLE
	-NOBLOCKMAP
	+SHOOTABLE
	-NOGRAVITY
	+NOEXPLODEFLOOR
	Height 10
	Radius 5
	Health 1
	damagefactor "Normal", 0.0
	damagefactor "Explosive", 1.0
	damagetype "Explosive"
	reactiontime 105
	States
	{
	Spawn:
		SUBD JKL 1 A_CountDown
		loop
	Death:
		SUBD A 0 A_PlaySoundEx("weapon/dynamiteexplode1", "body")
		SUBD A 0 A_Explode(25, 128, XF_HURTSOURCE, true, 128)
		SUBD A 0 A_SpawnItemEx("DynamiteBoom")
		SUBD AA 0 A_SpawnItemEx("DynamiteBoom", random(-16, 16), random(-16, 16), random(-16, 16))
		SUBD AAAAAA 0 A_SpawnItemEx("DynamiteBoomAdd", random(-32, 32), random(-32, 32), random(-32, 32))
		SUBD A 0 A_SpawnItemEx("DynamiteQuake")
		stop
	}
}

actor DynamiteBoom : BasicEffect
{
	Scale 2.5
	States
	{
	Spawn:
		SUBD MNOPQRSTUVWXY 1 bright
		stop
	}
}

actor DynamiteBoomAdd : BasicEffect
{
	renderstyle add
	alpha 0.6
	Scale 2.5
	States
	{
	Spawn:
		SUBD MNOPQRSTUVWXY 1 bright
		stop
	}
}

actor DynamiteQuake
{
	+NOINTERACTION
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 Radius_Quake(3, 10, 0, 50, 0)
		TNT1 A 10
		stop
	}
}

actor DynamiteAmmo : CustomInventory
{
	inventory.pickupsound "ammo/pickup"
	inventory.pickupmessage ""
	States
	{
	Spawn:
		SUBD J 1
		wait
	Pickup:
		TNT1 A 0 A_JumpIf(CallACS("status_ammo_check", SUB_DYNAMITE), "Success")
		fail
	Success:
		TNT1 A 0 ACS_NamedExecuteAlways("status_ammo", 0, SUB_DYNAMITE, 1)
		stop
	}
}

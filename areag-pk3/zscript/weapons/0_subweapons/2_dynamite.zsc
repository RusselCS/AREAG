class DynamiteEquipped : Once {}
class DynamiteAmmo : Counter {}
class DynamiteMax : Counter {}

class DynamiteCratePickup : SubWeaponPickup
{
	Default
	{
		scale 1.0;
	}
	States
	{
	Spawn:
		SUBD A 1;
		loop;
	Pickup:
		TNT1 A 0 ACS_NamedExecuteAlways("pickups_displayMessage", 0, PICKTYPE_SUBWEAPON, PICKMSG_DYNAMITE);
		TNT1 A 0 A_AREAGSelectWeapon(SWEP_DYNAMITE);
		TNT1 A 0 A_GiveInventory("DynamiteMax", 10);
		TNT1 A 0 A_GiveInventory("DynamiteAmmo", 10);
		stop;
	}
}

extend class AREAGProtoWeapon {

    action State A_AREAGDynamiteActive() {

            int wep = ZSCPlayerPawn(self).iSelectedWep;

            if(invoker.bLowerSub || (wep != SWEP_DYNAMITE || CountInv("DynamiteAmmo") == 0)) {
                return ResolveState("DynamiteLower");
            }

            return ResolveState(null);
    }

    States
    {
        DynamiteRaise:
            SDHD DCBA 1;
        DynamiteReady:
            SDHD A 1 A_AREAGDynamiteActive();
            loop;
        DynamiteThrow:
            HBMA A 0 A_PlaySound("weapon/dynamitethrow", CHAN_WEAPON);
            HBMA A 0 A_TakeInventory("DynamiteAmmo", 1);
            HBMA A 0 A_FireProjectile("DynamiteProjectile", 0, 0, 0, 0);
            HBMA ABCDE 1;
            TNT1 A 6;
            goto AltFireEnd;
        DynamiteLower:
            SDHD ABCD 1;
            goto Flash;
    }
}

class DynamiteProjectile : BasicProjectile
{
    Default
    {
        PROJECTILE;
        +RIPPER;
        -NOGRAVITY;
        gravity 1.2;
        wallbouncefactor 0.8;
        bouncefactor 0.4;
        bouncecount 3;
        height 10;
        radius 5;
        speed 40;
        bouncetype "doom";
    }
	States
	{
	Spawn:
		SUBD BCDEFGHI 1;
		loop;
	Death:
		SUBD B 0 A_SpawnItemEx("DynamiteMine");
		stop;
	}
}

class DynamiteMine : BasicProjectile
{
    Default
    {
        PROJECTILE;
        +THRUACTORS;
        +SHOOTABLE;
        -NOBLOCKMAP;
        +SHOOTABLE;
        -NOGRAVITY;
        +NOEXPLODEFLOOR;
        Height 10;
        Radius 5;
        Health 1;
        damagefactor "Normal", 0.0;
        damagefactor "Explosive", 1.0;
        damagetype "Explosive";
        reactiontime 105;
    }
	States
	{
	Spawn:
		SUBD JKL 1 A_CountDown;
		loop;
	Death:
		SUBD A 0 A_PlaySound("weapon/dynamiteexplode1", CHAN_BODY);
		SUBD A 0 A_Explode(25, 128, XF_HURTSOURCE, true, 128);
		SUBD A 0 A_SpawnItemEx("DynamiteBoom");
		SUBD AA 0 A_SpawnItemEx("DynamiteBoom", random(-16, 16), random(-16, 16), random(-16, 16));
		SUBD AAAAAA 0 A_SpawnItemEx("DynamiteBoomAdd", random(-32, 32), random(-32, 32), random(-32, 32));
		SUBD A 0 A_SpawnItemEx("DynamiteQuake");
		stop;
	}
}

class DynamiteBoom : BasicEffect
{
    Default
    {
        Scale 2.5;
    }
	States
	{
	Spawn:
		SUBD MNOPQRSTUVWXY 1 bright;
		stop;
	}
}

class DynamiteBoomAdd : DynamiteBoom
{
	Default
    {
        renderstyle "add";
	    alpha 0.6;
    }
}

class DynamiteQuake : Actor
{
    Default
    {
        +NOINTERACTION;
	    +NOCLIP;
    }
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 Radius_Quake(3, 10, 0, 50, 0);
		TNT1 A 10;
		stop;
	}
}
